#!/usr/bin/env groovy
// Pipeline for docker-image build
//  created by richb@instantlinux.net 20-apr-2017

def maintainer = 'richb@instantlinux.net'
def registry = 'nexus.instantlinux.net'
def registry_creds = [credentialsId: 'docker-registry',
                      url: "https://${registry}"]
def service = env.JOB_NAME.split('/', 2)[0]

pipeline {
  agent { label 'swarm' }
  stages {
    stage('Static Code Analysis') {
      steps {
        sh "env ; cd images/${service} && make analysis"
      }
    }
    stage('Create Image') {
      steps {
        script {
          gitCommit = checkout(scm).GIT_COMMIT.take(7)
          imageTag = "dev_build_${env.BUILD_NUMBER}_${gitCommit}"
          dir("images/${service}") {
            img = docker.build("${registry}/${service}:${imageTag}")
          }
        }
      }
    }
    stage('Push Image') {
      steps {
        withDockerRegistry(registry_creds) {
          script {
            img.push imageTag
          }
        }
      }
    }
    stage('Functional Tests') {
      steps {
        withDockerRegistry(registry_creds) {
          script {
            dir("images/${service}") {
              sh 'make test_functional'
            }
          }
        }
      }
    }
    stage('Promote Image') {
      steps {
        withDockerRegistry(registry_creds) {
          script {
            img.push 'latest'
          }
        }
      }
    }
  }
  post {
    always {
      deleteDir()
    }
    success {
      emailext (
        to: maintainer,
        subject: "Job ${env.JOB_NAME} #${env.BUILD_NUMBER} success",
        body: "Build URL: ${env.BUILD_URL}.\nDocker Image ${registry}/${service}\n",
        attachLog: true
      )
    }
    failure {
      sh "docker rmi ${registry}/${service}:${imageTag}"
      emailext (
        to: maintainer,
        subject: "Job ${env.JOB_NAME} #${env.BUILD_NUMBER} failed",
        body: "Failed build URL: ${env.BUILD_URL}.\n",
        attachLog: true
      )
    }
  }
}
